getabspath() {
	__=`
		awk -- '
			BEGIN {
				PATH = ARGV[1]

				if (ARGV[1] !~ "^[/]")
					PATH = ARGV[2] "/" PATH

				FS = "/"
				$0 = PATH

				T = 0

				for (F = 1; F <= NF; F++) {
						if ($F == "." || $F == "") {
						continue
					} else if ($F == "..") {
						if (T)
							--T
					} else {
						TOKENS[T++]=$F
					}
				}

				if (T) {
					for (I = 0; I < T; I++)
						ABS = ABS "/" TOKENS[I]
					if (PATH ~ /\/$/)
						ABS = ABS "/"
				} else if (PATH ~ /\/$/) {
					ABS = "/"
				} else {
					ABS = "/."
				}

				print ABS

				exit
			}
		' "$1" "`exec pwd`"
	`
}
